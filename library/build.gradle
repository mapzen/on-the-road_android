buildscript {
  ext.kotlin_version = '1.0.6'

  repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'net.researchgate:gradle-release:2.4.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'net.researchgate.release'

group = GROUP
version = VERSION_NAME
project.archivesBaseName = POM_ARTIFACT_ID

release {
  tagTemplate = 'on-the-road-${version}'
  versionPropertyFile = '../gradle.properties'
  versionProperties = ['VERSION_NAME']
}

task copyTask(type: Copy) {
  println "Copying com/mapzen/ontheroad/R.java to android/support/v7/appcompat/R.java"
  from 'build/generated/source/r/debug/com/mapzen/ontheroad'
  into 'build/generated/source/r/debug/android/support/v7/appcompat'
  include 'R.java'
  filter { line ->
    line.contains('package com.mapzen.ontheroad;') ? 'package android.support.v7.appcompat;' : line
  }
}

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    versionCode 1
    versionName version
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    abortOnError false
  }
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "started", "skipped", "passed", "failed"
    showStandardStreams true
  }
}

repositories {
  jcenter()
  maven { url 'http://repo.maven.apache.org/maven2' }
  mavenCentral()
}

dependencies {
  compile 'com.android.support:appcompat-v7:25.1.0'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile 'com.squareup.okhttp3:okhttp:3.5.0'
  compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
  compile 'org.apache.commons:commons-io:1.3.2'
  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:converter-gson:2.1.0'
  compile 'com.squareup.retrofit2:converter-scalars:2.1.0'

  compile 'com.f2prateek.ln:ln:1.1.1'
  testCompile 'junit:junit:4.12'
  testCompile('org.robolectric:robolectric:2.2') {
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  testCompile 'com.squareup:fest-android:1.0.7'
  testCompile 'org.mockito:mockito-all:1.9.5'
  testCompile 'com.squareup.okhttp3:mockwebserver:3.5.0'
  testCompile 'org.easytesting:fest-reflect:1.4.1'
  testCompile 'com.google.guava:guava:18.0'
  testCompile 'org.json:json:20160212'
  testCompile 'org.apache.httpcomponents:httpclient:4.5.3'
}

apply from: file('../gradle-mvn-push.gradle')
